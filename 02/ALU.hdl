// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // For zx and nx -- to avoid negation and zero input
    Mux16(a=x, b=false, sel=zx, out=x1);
    Not16(in=x1, out=notx1);
    Mux16(a=x1, b=notx1, sel=nx, out=x2);
    // For xy and ny -- same as above
    Mux16(a=y, b=false, sel=zy, out=y1);
    Not16(in=y1, out=noty1);
    Mux16(a=y1, b=noty1, sel=ny, out=y2);
    // For f, and the selections 1 == x + y, 0 == x & y
    // With Mux, a=0 (or x & y), b=1 (or x + y)
    // Afterwhich you do the not, for the 'no' input to select later on
    Add16(a=x2, b=y2, out=addxy);
    And16(a=x2, b=y2, out=andxy);
    Mux16(a=andxy, b=addxy, sel=f, out=f1);
    Not16(in=f1, out=notf1);
    // For no, to find the output
    // Naming the out o1, and splitting it into busses
    // zr is found by breaking into chunks to perform bitwise OR operations on the whole bus
    // ng is found by doing an AND operation on bit(n-1)
    Mux16(a=f1, b=notf1, sel=no, out=out, out[0..7]=out7, out[8..15]=out15, out[15]=ngx);
    // zr
    Or8Way(in=out7, out=o2);
    Or8Way(in=out15, out=o3);
    Or(a=o2, b=o3, out=zr0);
    // This is the out that determines zr, the final output from the bitwise OR operation
    Not(in=zr0, out=zr);
    // ng
    And(a=ngx, b=true, out=ng);
}